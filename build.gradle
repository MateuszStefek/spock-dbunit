buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
    id 'groovy'
    id 'maven-publish'
}

apply from: "gradle/libraries.gradle"


group = 'be.janbols'
version = "0.2"

sourceCompatibility = 1.6
targetCompatibility = 1.6

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


repositories {
    mavenLocal()
    mavenCentral()
}


dependencies {
    compile libs.groovy
    compile libs.logging.api
    compile libs.spock.core
    compile libs.dbunit

    runtime libs.logging.impl

    testCompile libs.spring.base
    testCompile libs.spring.test
    testCompile libs.jodatime
    testCompile libs.h2
    testCompile libs.spock.spring
    testCompile libs.datasource
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = "2.6"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ["mavenJava"]
    pkg {
        repo = 'maven'
        name = 'spock-dbunit'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/janbols/spock-dbunit'
        vcsUrl = 'https://github.com/janbols/spock-dbunit.git'
        version {
            name = version
            vcsTag = version
        }
    }

}
bintrayUpload.dependsOn build, sourcesJar